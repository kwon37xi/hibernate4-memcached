buildscript {
    repositories {
        mavenCentral()
    }
}

ext {
    javaVersion = '1.6'
    hibernateVersion = '4.2.12.Final'
    lz4Version = '1.2.0'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    group = 'kr.pe.kwonnam.hibernate4memcached'
    version = '0.4-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api:1.7.7'

        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.5'
        testCompile 'org.easytesting:fest-assert-core:2.0M10'
        testCompile 'ch.qos.logback:logback-classic:1.0.13' // DO NOT upgrade to 1.1.x. sometimes logs disapear.
    }

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    tasks.withType(Compile) {
        options.encoding = 'UTF-8'
    }

    jacoco {
        toolVersion = '0.7.1.201405082137'
    }

    test {
        jacoco {
            enabled = true
        }
    }
}

configure(subprojects.findAll { ['hibernate4-memcached-core', 'hibernate4-memcached-spymemcached-adapter'].contains(it.name) }) {
    apply plugin: 'maven'
    apply plugin: 'signing'

    // from http://central.sonatype.org/pages/gradle.html

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name "${project.name}"
                    packaging 'jar'
                    description 'hibernate4 memcached L2 cache implementation.'
                    url 'https://github.com/kwon37xi/hibernate4-memcached'

                    scm {
                        connection 'scm:git:https://github.com/kwon37xi/hibernate4-memcached.git'
                        url 'https://github.com/kwon37xi/hibernate4-memcached'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'kwon37xi'
                            name 'KwonNam Son'
                            email 'kwon37xi@gmail.com'
                        }
                    }
                }
            }
        }
    }
}